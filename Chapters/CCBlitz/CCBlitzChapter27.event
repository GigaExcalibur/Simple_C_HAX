#ifndef CCBLITZ_STAN_CH27
#define CCBLITZ_STAN_CH27

/*

I hope y'all like Fog of war Desert Maps with one-tile hidden treasure
and wind streams taken straight out of Fuga's wild ride =^)

*/

#include "EAstdlib.event"

#include "CCBlitzChapter27Asmcs.lyn.event"

#ifndef SetSymbolDefined
	#define SetSymbolDefined

	#define SetSymbol(aName, aValue) "PUSH; ORG (aValue); aName:; POP"
//	#define SetSymbol(aName, aValue) "aName = (aValue)"
#endif // SetSymbolDefined

#ifndef IsPointer
	#define IsPointer 0x8000000
#endif // IsPointer

/* // :(
#ifndef RedaPoolDefined
	#define RedaPoolDefined

	#define InlReda(aXTarget, aYTarget) "AddToPool(REDA [(aXTarget), (aYTarget)] 0 0 0 0)"
//	#define InlReda(aXTarget, aYTarget) "REDA ##Â aXTarget ## R ## aYTarget" // It almost could work!
#endif // RedaPoolDefined
*/

SetSymbol(scPrepScreen,         $591FD8) // no args
SetSymbol(scNoSkipFadeOut,      $591F40) // no args
SetSymbol(scLoadReinforcements, $9EE274) // args: s2 = pointer to UNITs

// ==============
// CHAPTER CONFIG
// ==============

#define Chapter27Id         0x00
#define Chapter27Map        0x49
#define Chapter27MapChanges 0x4A
#define Chapter27Events     0x07

#define Ch27LvBase(aLevelModifier) "Level((15 + (aLevelModifier)), Enemy, True)"
#define Ch27LvProm(aLevelModifier) "Level((10 + (aLevelModifier)), Enemy, True)"
#define Ch27LvBoss(aLevelModifier) "Level((15 + (aLevelModifier)), Enemy, True)"

#define Ch27MonsterSpawnCoords "[10, 12]"
#define Ch27MonsterSpawnCoordsRaw 0xC000A

// Character Indices
#define Ch27ChrMGeneric    0x80 // Monster
#define Ch27ChrHGeneric    0x81 // Human

// Local Event Id Allocation
SetSymbol(eidEnemyDeath,       10)
SetSymbol(eidVillage,          11)
SetSymbol(eidHumanReinf,       12)
SetSymbol(eidHumanReinfActive, 13)

// Event Counter Allocation
SetSymbol(cntKillCountLo,   0) // low 4 bits of total kill count
SetSymbol(cntKillCountHi,   1) // high 4 bits of total kill count
SetSymbol(cntKillCountTurn, 2) // this turn's kill count
SetSymbol(cntMonsterCycle,  3) // what's the next monster slot to load
SetSymbol(cntHumanCycle,    4) // what's the next human slot to load
SetSymbol(cntHumanCounter,  5) // when the next human reinf comes

// ============
// CHAPTER DATA
// ============

// Temporary hack because >Editing the death quote table
// We need to have the generics have a death quote that sets eidEnemyDeath

PUSH; ORG $9ECD4C
	SHORT Ch27ChrMGeneric; BYTE 0xFF 0xFF; SHORT eidEnemyDeath; SHORT 0; WORD 0
	SHORT Ch27ChrHGeneric; BYTE 0xFF 0xFF; SHORT eidEnemyDeath; SHORT 0; WORD 0

	SHORT 0xFFFF
POP

EventPointerTable(Chapter27Events, ThisChapter)

ALIGN 4
ThisChapter:
	POIN TurnEventList
	POIN CharacterEventList
	POIN LocationEventList
	POIN ActionEventList
	POIN DunnoList DunnoList DunnoList
	POIN TutorialList
	POIN TrapList TrapList2
	POIN unPlayerList unPlayerList
	WORD 0 0 0 0 0 0
	POIN scBeginning scEnding

ALIGN 4
TurnEventList:
	TURN 0 scShiftSands       [0, 0xFF] 0 // Shift sands every turn
	TURN 0 scTurnMonsterReinf [0, 0xFF] 0 // Call monster reinf every turn

	TURN eidHumanReinf scHumanReinf [0, 0xFF] 0
	TURN 0 scHumanReinfFurther [0, 0xFF] 0

	END_MAIN

ALIGN 4
CharacterEventList:
	END_MAIN

ALIGN 4
LocationEventList:
	Village(eidVillage, scVillageVisit, 2, 4)

	END_MAIN

ALIGN 4
ActionEventList:
	AFEV 0 scOnEnemyDeath eidEnemyDeath

	// TODO: hidden treasure!
	// TODO: mark them on the map with different tiles

	END_MAIN

ALIGN 4
DunnoList:
	END_MAIN

ALIGN 4
TutorialList:
	WORD 0

ALIGN 4
TrapList:
TrapList2:
	ENDTRAP

ALIGN 4
scResetKillCount: {
	COUNTER_SET cntKillCountLo 0
	COUNTER_SET cntKillCountHi 0

	NoFade
	ENDA
}

ALIGN 4
scIncKillCount: {
	SetSymbol(inc_regular, 0)
	SetSymbol(end,         1)

	COUNTER_CHECK cntKillCountLo // sC = LoKillCount
	SVAL r7 15 // 15 is max for any counter (because 4 bits)

	BNE inc_regular rC r7

	COUNTER_SET cntKillCountLo 0

	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an increment instruction, not an addition
	COUNTER_ADD cntKillCountHi 0

	GOTO end

LABEL inc_regular
	COUNTER_ADD cntKillCountLo 0

LABEL end
	NoFade
	ENDA
}

ALIGN 4
scGetKillCount: {
	COUNTER_CHECK cntKillCountHi

	// r1 = KillCountHi << 4
	SVAL r2 4
	SLSL slotsParam(r1, rC, r2)

	COUNTER_CHECK cntKillCountLo

	// rC = (KillCountHi << 4) + KillCountLo
	SADD slotsParam(rC, rC, r1)

	NoFade
	ENDA
}

ALIGN 4
scGetNextMonsterIndex: {
	SetSymbol(inc, 0)
	SetSymbol(end, 1)

	COUNTER_CHECK cntMonsterCycle

	SVAL r1 15
	BNE inc rC r1

	COUNTER_SET cntMonsterCycle 0

LABEL inc
	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an increment instruction, not an addition
	COUNTER_ADD cntMonsterCycle 0

LABEL end
	COUNTER_CHECK cntMonsterCycle

	NoFade
	ENDA
}

ALIGN 4
scGetNextHumanGroupIndex: {
	SetSymbol(inc, 0)
	SetSymbol(end, 1)

	COUNTER_CHECK cntHumanCycle

	SVAL r1 4
	BNE inc rC r1

	COUNTER_SET cntHumanCycle 0

LABEL inc
	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an increment instruction, not an addition
	COUNTER_ADD cntHumanCycle 0

LABEL end
	COUNTER_CHECK cntHumanCycle

	NoFade
	ENDA
}

ALIGN 4
scGetMonsterCoordsForShift: {
	// Shift index in s2

	SVAL r3 (IsPointer | MonsterWalkCoordLookup)
	ASMC GetCoordsFromIndexASMC

	NoFade
	ENDA

ALIGN 4
MonsterWalkCoordLookup:
	BYTE 15 17 // Shift 0  -> 1
	BYTE 10 18 // Shift 1  -> 2
	BYTE  6 16 // Shift 2  -> 3
	BYTE  4 13 // Shift 3  -> 4
	BYTE  5  9 // Shift 4  -> 5
	BYTE  6  6 // Shift 5  -> 6
	BYTE  9  3 // Shift 6  -> 7
	BYTE 12  4 // Shift 7  -> 8
	BYTE 14  8 // Shift 8  -> 9
	BYTE 16 10 // Shift 9  -> 10
	BYTE 19 12 // Shift 10 -> 11
	BYTE 17 15 // Shift 11 -> 0

}

ALIGN 4
scBeginning: {
	VCWF 4 // FOG
	WEA1 6 // SANDSTORM

	// Initial tile changes
	TILECHANGE 1
	TILECHANGE 7

	// Initial event id states
	// ENUT eidHumanReinf

//	COUNTER_SET cntKillCountTurn 15

	CALL scResetKillCount

	LOAD1 0 unPlayerList
	ENUN

	CALL scNoSkipFadeOut
	CALL scPrepScreen

	NoFade
	ENDA
}

ALIGN 4
scVillageVisit: {
	// TODO

	NoFade
	ENDA
}

ALIGN 4
scShiftSands: {
	SetSymbol(not_turn1, 0)
	SetSymbol(not_turn2, 1)
	SetSymbol(not_turn3, 2)
	SetSymbol(not_turn4, 3)
	SetSymbol(not_turn5, 4)
	SetSymbol(not_turn6, 5)
	SetSymbol(end,       6)

	ASMC SlideAllUnitsASMC

	EARTHQUAKE_START 0

	// TODO (polish): play windy sound?

	CHECK_TURNS

	SVAL r7 6
	SMOD slotsParam(r1, rC, r7)

	SVAL r7 1
	BNE not_turn1 r1 r7

	// Turn 1 - 7 - 13 - 19...

	CAM1 [18, 12]

	SVAL r2 (IsPointer | mclShift1)
	ASMC MapChangeListASMC

	CAM1 [4, 7]

	SVAL r2 (IsPointer | mclShift7)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn1
	SVAL r7 2
	BNE not_turn2 r1 r7

	// Turn 2 - 8 - 14 - 20...

	CAM1 [16, 16]

	SVAL r2 (IsPointer | mclShift2)
	ASMC MapChangeListASMC

	CAM1 [5, 5]

	SVAL r2 (IsPointer | mclShift8)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn2
	SVAL r7 3
	BNE not_turn3 r1 r7

	// Turn 3 - 9 - 15 - 21...

	CAM1 [12, 17]

	SVAL r2 (IsPointer | mclShift3)
	ASMC MapChangeListASMC

	CAM1 [8, 3]

	SVAL r2 (IsPointer | mclShift9)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn3
	SVAL r7 4
	BNE not_turn4 r1 r7

	// Turn 4 - 10 - 16 - 22...

	CAM1 [8, 17]

	SVAL r2 (IsPointer | mclShift4)
	ASMC MapChangeListASMC

	CAM1 [13, 4]

	SVAL r2 (IsPointer | mclShift10)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn4
	SVAL r7 5
	BNE not_turn5 r1 r7

	// Turn 5 - 11 - 17 - 23...

	CAM1 [4, 15]

	SVAL r2 (IsPointer | mclShift5)
	ASMC MapChangeListASMC

	CAM1 [14, 6]

	SVAL r2 (IsPointer | mclShift11)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn5
	SVAL r7 0
	BNE not_turn6 r1 r7

	// Turn 6 - 12 - 18 - 24...

	CAM1 [4, 12]

	SVAL r2 (IsPointer | mclShift6)
	ASMC MapChangeListASMC

	CAM1 [17, 9]

	SVAL r2 (IsPointer | mclShift12)
	ASMC MapChangeListASMC

	GOTO end

LABEL not_turn6
LABEL end
	EARTHQUAKE_END

	NoFade
	ENDA
}

ALIGN 4
scOnEnemyDeath: {
	ENUF eidEnemyDeath // Allow the next enemy to also die

	CALL scIncKillCount
	CALL scHandleKillEvents

	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an increment instruction, not an addition
	COUNTER_ADD cntKillCountTurn 0

#ifdef __DEBUG__
	// Give the amount of enemies killed as money
	// Easy way to keep track of the number

	CALL scGetKillCount
	SADD slotsParam(r3, rC, r0)

	// Gib Mone
	GIVEITEMTOMAIN 0
#endif // __DEBUG__

	NoFade
	ENDA
}

ALIGN 4
scTurnMonsterReinf: {
	SetSymbol(loop, 0)
	SetSymbol(end,  1)

	SVAL r2 Ch27ChrMGeneric
	ASMC BuffAllCharactersASMC

	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an increment instruction, not an addition
	COUNTER_ADD cntKillCountTurn 0

	COUNTER_CHECK cntKillCountTurn
	BEQ end rC r0

LABEL loop
	CALL scGetNextMonsterIndex

	SADD slotsParam(r2, rC, r0)
	SVAL r3 (IsPointer | MonsterUnitLookup)

	// ldr s2, [s3, s2, lsl #2]
	ASMC LoadIndexedWordAMSC
	SADD slotsParam(r2, rC, r0)

	CALL scLoadReinforcements

	// Because Intelligent Systems/10, The Unit map doesn't work
	// for units that are in the fog unless it is enemy phase.
	// To address that issue, I'm silently disabling fog for the duration
	// of the MOVEFORCED prep & call, and restoring immediately after.

	ASMC FogHackBeginASMC

	CHECK_TURNS

	SVAL r2 1
	SSUB slotsParam(rC, rC, r2)

	SVAL r2 12
	SMOD slotsParam(r2, rC, r2)

	CALL scGetMonsterCoordsForShift

	SADD slotsParam(r2, rC, r0) 
	SVAL rB Ch27MonsterSpawnCoordsRaw

	ASMC GetBestCoordsForUnitAtASMC

	SVAL rD 0

	SVAL r1 0xFFFF
	SAND slotsParam(r1, rC, r1)

	SVAL r2 16
	SLSR slotsParam(r2, rC, r2)

	SVAL r3 6
	SLSL slotsParam(r3, r2, r3)

	// Slot1 = (Coords & 0xFFFF) | (Coords >> 16 << 6);
	SORR slotsParam(r1, r1, r3)
	SAVETOQUEUE

	SVAL r1 0
	SAVETOQUEUE

	MOVEFORCED 0 UnitAtCoordsSlotB 0

	ASMC FogHackEndASMC

	ENUN

	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an decrement instruction, not a substraction
	COUNTER_SUBTRACT cntKillCountTurn 0
	COUNTER_SUBTRACT cntKillCountTurn 0

	COUNTER_CHECK cntKillCountTurn
	BNE loop rC r0

LABEL end
	NoFade
	ENDA

ALIGN 4
MonsterUnitLookup:
	POIN unEnemyMonster0
	POIN unEnemyMonster1
	POIN unEnemyMonster2
	POIN unEnemyMonster3
	POIN unEnemyMonster4
	POIN unEnemyMonster5
	POIN unEnemyMonster6
	POIN unEnemyMonster7
	POIN unEnemyMonster8
	POIN unEnemyMonster9
	POIN unEnemyMonster10
	POIN unEnemyMonster11
	POIN unEnemyMonster12
	POIN unEnemyMonster13
	POIN unEnemyMonster14
	POIN unEnemyMonster15

}

ALIGN 4
scHandleKillEvents: {
	// TODO

	NoFade
	ENDA
}

ALIGN 4
scHumanReinf: {
	CAM1 [17, 2]

	LOAD1 0 unEnemyHumanInitialTorcher
	ENUN

	// [20, 2]
	SVAL rB 0x20014

	ASMC TorchStaffAnimASMC
	ASMC TorchSetASMC

	STAL 20

	LOAD1 0 unEnemyHumanInitialSandCleaner
	ENUN

	// TODO (polish): have mage animate and cast spell
	// TODO (polish): play windy sound?

	EARTHQUAKE_START 0

	STAL 30

	TILECHANGE 110

	STAL 30

	TILECHANGE 111

	STAL 30

	TILECHANGE 112

	STAL 30

	EARTHQUAKE_END

	LOAD1 0 unEnemyHumanInitialWave
	ENUN

	ENUT eidHumanReinfActive

	COUNTER_SET cntHumanCounter 2

	NoFade
	ENDA
}

ALIGN 4
scHumanReinfFurther: {
	SetSymbol(end, 0)

	CHECK_EVENTID eidHumanReinfActive
	BEQ end rC r0

	// EA Raw is broken, there's only one argument and it's the counted id
	// This is an decrement instruction, not a substraction
	COUNTER_SUBTRACT cntHumanCounter 0

	COUNTER_CHECK cntHumanCounter
	BNE end rC r0

	CALL scGetNextHumanGroupIndex

	SADD slotsParam(r2, rC, r0)
	SVAL r3 (IsPointer | HumanUnitGroupLookup)

	ASMC LoadIndexedWordAMSC
	SADD slotsParam(r2, rC, r0)

	CALL scLoadReinforcements

	COUNTER_SET cntHumanCounter 2

LABEL end
	NoFade
	ENDA

ALIGN 4
HumanUnitGroupLookup:
	POIN unEnemyHumanWave0
	POIN unEnemyHumanWave1
	POIN unEnemyHumanWave2
	POIN unEnemyHumanWave3
	POIN unEnemyHumanWave4

}

ALIGN 4
scEnding: {
	// move to next chapter whatever it is
	MNC2 1

	ENDA
}

// Those are tile change lists to be used with the ASMC
// I use the asmc instead of the regular TILECHANGE/TILEREVERT events
// because I want to display the tile change fade for more than one at the time
// otherwise it would look kind of weird (especially for Shift7, that's in 2 parts)

mclShift1:
	SHORT 1 False
	SHORT 2 True
	SHORT (-1)

mclShift2:
	SHORT 2 False
	SHORT 3 True
	SHORT (-1)

mclShift3:
	SHORT 3 False
	SHORT 4 True
	SHORT (-1)

mclShift4:
	SHORT 4 False
	SHORT 5 True
	SHORT (-1)

mclShift5:
	SHORT 5 False
	SHORT 6 True
	SHORT (-1)

mclShift6:
	SHORT 6 False
	SHORT 7 True
	SHORT (-1)

mclShift7:
	SHORT 7  False
	SHORT 8  True
	SHORT 82 True
	SHORT (-1)

mclShift8:
	SHORT 8  False
	SHORT 82 False
	SHORT 9  True
	SHORT (-1)

mclShift9:
	SHORT 9  False
	SHORT 10 True
	SHORT (-1)

mclShift10:
	SHORT 10 False
	SHORT 11 True
	SHORT (-1)

mclShift11:
	SHORT 11 False
	SHORT 12 True
	SHORT (-1)

mclShift12:
	SHORT 12 False
	SHORT 1  True
	SHORT (-1)

// ==================
// CHAPTER UNIT LISTS
// ==================

ALIGN 4
unPlayerList:
	// Feature Testing Crew

	UNIT Eirika   FalcoKnight    0 Level(20, Ally, True) [19, 13] 0 0 0 0 [Sieglinde, Runesword, MemberCard]     NoAI
	UNIT Moulder  Bishop         0 Level(20, Ally, True) [20, 12] 0 0 0 0 [Latona, Aura, Mend]                   NoAI
	UNIT Innes    Sniper         0 Level(20, Ally, True) [17, 12] 0 0 0 0 [Nidhogg, Longbow]                     NoAI
	UNIT LArachel MageKnight_F   0 Level(20, Ally, True) [21, 14] 0 0 0 0 [Excalibur, Heal, Vulnerary]           NoAI
	UNIT Gilliam  General        0 Level(20, Ally, True) [18, 14] 0 0 0 0 [Garm, Javelin]                        NoAI
	UNIT Seth     Paladin        0 Level(20, Ally, True) [16, 12] 0 0 0 0 [SilverSword, SilverLance, Elixir]     NoAI
	UNIT Lute     Sage_F         0 Level(20, Ally, True) [16, 13] 0 0 0 0 [Ivaldi, Thunder, Warp]                NoAI
	UNIT Tana     WyvernKnight_F 0 Level(20, Ally, True) [17, 14] 0 0 0 0 [Vidofnir, Spear, Elixir]              NoAI
	UNIT Ewan     Summoner       0 Level(20, Ally, True) [18, 13] 0 0 0 0 [Gleipnir, Flux, Nosferatu, Rescue]    NoAI
	UNIT Marisa   Swordmaster_F  0 Level(20, Ally, True) [20, 14] 0 0 0 0 [Audhulma, BraveSword, Shamshir]       NoAI
	UNIT Tethys   Dancer         0 Level(20, Ally, True) [21, 13] 0 0 0 0 [Elixir, PureWater]                    NoAI

	UNIT // end

ALIGN 4
unEnemyMonster0:
	UNIT Ch27ChrMGeneric Bonewalker 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverSword] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster1:
	UNIT Ch27ChrMGeneric Revenant 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [FetidClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster2:
	UNIT Ch27ChrMGeneric Bonewalker 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [Lancereaver] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster3:
	UNIT Ch27ChrMGeneric Entombed 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [FetidClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster4:
	UNIT Ch27ChrMGeneric Bonewalker_Bow 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverBow] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster5:
	UNIT Ch27ChrMGeneric Wight 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverSword] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster6:
	UNIT Ch27ChrMGeneric Revenant 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [PoisonClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster7:
	UNIT Ch27ChrMGeneric Cyclops 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverAxe] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster8:
	UNIT Ch27ChrMGeneric Wight_Bow 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverBow] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster9:
	UNIT Ch27ChrMGeneric Revenant 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [PoisonClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster10:
	UNIT Ch27ChrMGeneric Entombed 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [FetidClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster11:
	UNIT Ch27ChrMGeneric Wight 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverSword] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster12:
	UNIT Ch27ChrMGeneric Cyclops 0 Ch27LvProm(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverAxe] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster13:
	UNIT Ch27ChrMGeneric Bonewalker 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [SilverLance] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster14:
	UNIT Ch27ChrMGeneric Bonewalker 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [Axereaver] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyMonster15:
	UNIT Ch27ChrMGeneric Revenant 0 Ch27LvBase(1) Ch27MonsterSpawnCoords 0 0 0 0 [PoisonClaw] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyHumanInitialTorcher:
	UNIT Ch27ChrHGeneric Bishop 0 Ch27LvProm(3) [20, 0] 2 0 1 redaHumanCenterLeft [Aura, TorchStaff, Sleep] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyHumanInitialSandCleaner:
	UNIT Ch27ChrHGeneric Sage 0 Ch27LvProm(3) [21, 0] 0 0 1 redaHumanCenterRight [Fimbulvetr] PursueWithoutHeedAI
	UNIT // end

ALIGN 4
unEnemyHumanInitialWave:
	UNIT Ch27ChrHGeneric Mage 0 Ch27LvBase(3) [19, 0] 0 0 1 redaHumanBottomLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric Mage 0 Ch27LvBase(3) [22, 0] 0 0 1 redaHumanBottomRight [0] PursueWithoutHeedAI

	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(2) [20, 0] 0 0 1 redaHumanTopLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(2) [21, 0] 0 0 1 redaHumanTopRight [0] PursueWithoutHeedAI

	UNIT Ch27ChrHGeneric WyvernKnight 0 Ch27LvProm(2) [18, 0] 0 0 1 redaHumanLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric WyvernKnight 0 Ch27LvProm(2) [23, 0] 0 0 1 redaHumanRight [0] PursueWithoutHeedAI

	UNIT // end

ALIGN 4
unEnemyHumanWave0:
	UNIT Ch27ChrHGeneric Mage 0 Ch27LvBase(4) [19, 0] 0 0 1 redaHumanTopLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric Mage 0 Ch27LvBase(4) [22, 0] 0 0 1 redaHumanTopRight [0] PursueWithoutHeedAI

	UNIT // end

ALIGN 4
unEnemyHumanWave1:
	UNIT Ch27ChrHGeneric WyvernKnight 0 Ch27LvProm(3) [18, 0] 0 0 1 redaHumanLeft2  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric WyvernKnight 0 Ch27LvProm(3) [23, 0] 0 0 1 redaHumanRight2 [0] PursueWithoutHeedAI

	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(3) [20, 0] 0 0 1 redaHumanBottomLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(3) [21, 0] 0 0 1 redaHumanBottomRight [0] PursueWithoutHeedAI

	UNIT // end

ALIGN 4
unEnemyHumanWave2:
	UNIT Ch27ChrHGeneric Sage 0 Ch27LvProm(4) [20, 0] 0 0 1 redaHumanTopLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric Sage 0 Ch27LvProm(4) [21, 0] 0 0 1 redaHumanTopRight [0] PursueWithoutHeedAI

	UNIT // end

ALIGN 4
unEnemyHumanWave3:
	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(3) [20, 0] 0 0 1 redaHumanCenterLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric WyvernRider 0 Ch27LvBase(3) [21, 0] 0 0 1 redaHumanCenterRight [0] PursueWithoutHeedAI

	UNIT // end

ALIGN 4
unEnemyHumanWave4:
	UNIT Ch27ChrHGeneric FalcoKnight 0 Ch27LvProm(4) [19, 0] 0 0 1 redaHumanBottomLeft  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric FalcoKnight 0 Ch27LvProm(4) [22, 0] 0 0 1 redaHumanBottomRight [0] PursueWithoutHeedAI

	UNIT Ch27ChrHGeneric PegasusKnight 0 Ch27LvBase(4) [18, 0] 0 0 1 redaHumanLeft2  [0] PursueWithoutHeedAI
	UNIT Ch27ChrHGeneric PegasusKnight 0 Ch27LvBase(4) [23, 0] 0 0 1 redaHumanRight2 [0] PursueWithoutHeedAI

	UNIT // end

redaHumanCenterLeft:
	REDA [20, 2] 0 0 0 0

redaHumanCenterRight:
	REDA [21, 2] 0 0 0 0

redaHumanBottomLeft:
	REDA [19, 3] 0 0 0 0

redaHumanBottomRight:
	REDA [22, 3] 0 0 0 0

redaHumanTopLeft:
	REDA [19, 0] 0 0 0 0

redaHumanTopRight:
	REDA [22, 0] 0 0 0 0

redaHumanLeft:
	REDA [18, 1] 0 0 0 0

redaHumanRight:
	REDA [23, 1] 0 0 0 0

redaHumanLeft2:
	REDA [18, 2] 0 0 0 0

redaHumanRight2:
	REDA [23, 2] 0 0 0 0

#endif // CCBLITZ_STAN_CH27
